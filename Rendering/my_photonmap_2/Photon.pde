
// ----- Photon Mapping ----- Variables
int nrPhotons = 5000;             //Number of Photons Emitted
int nrBounces = 3;                //Number of Times Each Photon Bounces
boolean lightPhotons = true;      //Enable Photon Lighting?
int[][] numPhotons = {{0,0,0},{0,0,0,0,0}};              //Photon Count for Each Scene Object
float[][][][][] photons = new float[3][5][25000][3][3]; //Allocated Memory for Per-Object Photon Info


//Debug
//float sqRadius = 0.0005;             //Photon Integration Area (Squared for Efficiency)
//float exposure = 1.0;            //Number of Photons Integrated at Brightest Pixel


float sqRadius = 0.25;             //Photon Integration Area (Squared for Efficiency)
float exposure = 100.0;            //Number of Photons Integrated at Brightest Pixel


//---------------------------------------------------------------------------------------
//Photon Mapping ------------------------------------------------------------------------
//---------------------------------------------------------------------------------------

// !!! pzn - better solution would be to store photons independent of objects in space-sorted structure 

float[] gatherPhotons(float[] p, int type, int id){
  float[] energy = {0.0,0.0,0.0};  
  float[] N = surfaceNormal(type, id, p, gOrigin);                   //Surface Normal at Current Point of Current Object
  for (int i = 0; i < numPhotons[type][id]; i++){                    //Photons of Current Object
    if (gatedSqDist3(p,photons[type][id][i][0],sqRadius)){           //Is Photon Close to Point?
      float weight = max(0.0, -dot3(N, photons[type][id][i][1] ));   //Single Photon Diffuse Lighting
      //weight *= 2*(sqrt(sqRadius) - sqrt(gSqDist)) / exposure;                    //Weight by Photon-Point Distance - sphere mask
      //weight *= sqrt(sqRadius - gSqDist) / exposure;                    //Weight by Photon-Point Distance - sphere mask
      weight = 0.5/exposure;
      energy = add3(energy, mul3c(photons[type][id][i][2], weight)); //Add Photon's Energy to Total
   }} 
  return energy;
}

void emitPhotons(){
  //randomSeed(0); //Ensure Same Photons Each Time
  
  //Initialize Photon Count to Zero for Each Object
  for (int t = 0; t < nrTypes; t++)            
  for (int i = 0; i < nrObjects[t]; i++)
  numPhotons[t][i] = 0; 

  for (int i = 0; i <  nrPhotons; i++){
    int bounces = 1;
    float[] rgb = {1.0,1.0,1.0};               //Initial Photon Color is White
    float[] ray = normalize3( rand3(1.0) );    //Randomize Direction of Photon Emission
    
    // Emit From Point Light Source
    float[] prevPoint = Light;                 
    // Emit from volumetric source
    while (prevPoint[1] >= Light[1]) prevPoint = add3(Light, mul3c(normalize3(rand3(1.0)), 0.7));
      
    raytrace(ray, prevPoint);                          //Trace the Photon's Path
    
    // bounce photon LOOP
    while (gIntersect && bounces <= nrBounces){        
        rgb = mul3c (getColor(rgb,gType,gIndex), 1.0/sqrt(bounces));    // decay color by bounce
        gPoint = add3( mul3c(ray,gDist), prevPoint);   // intesection point
        storePhoton(gType, gIndex, gPoint, ray, rgb);  // Store Photon 
        ray = reflect(ray,prevPoint);                  // Bounce the Photon
        raytrace(ray, gPoint);                         // Trace It to Next Location
        
        prevPoint = gPoint;
        bounces++;
    }
    
  }
}

void storePhoton(int type, int id, float[] location, float[] direction, float[] energy){
  photons[type][id][numPhotons[type][id]][0] = location;  //Location
  photons[type][id][numPhotons[type][id]][1] = direction; //Direction
  photons[type][id][numPhotons[type][id]][2] = energy;    //Attenuated Energy (Color)
  numPhotons[type][id]++;
}












float[] filterColor(float[] rgbIn, float r, float g, float b){ //e.g. White Light Hits Red Wall
  float[] rgbOut = {r,g,b};
  for (int c=0; c<3; c++) rgbOut[c] = min(rgbOut[c],rgbIn[c]); //Absorb Some Wavelengths (R,G,B)
  return rgbOut;
}

float[] getColor(float[] rgbIn, int type, int index){ //Specifies Material Color of Each Object
  if      (type == 1 && index == 0) { return filterColor(rgbIn, 0.0, 1.0, 0.0);}
  else if (type == 1 && index == 2) { return filterColor(rgbIn, 1.0, 0.0, 0.0);}
  else                              { return filterColor(rgbIn, 1.0, 1.0, 1.0);}
}

